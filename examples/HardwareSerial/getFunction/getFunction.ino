// ПРИМЕР ПОЛУЧЕНИЯ И ВЫЧИСЛЕНИЯ ДАННЫХ:                                           //
                                                                                   //
#include <iarduino_Modbus.h>                                                       //   Подключаем библиотеку для работы по протоколу Modbus.
#include <iarduino_MB_TDS.h>                                                       //   Подключаем библиотеку для работы с TDS-метрами.
                                                                                   //
ModbusClient    modbus(Serial1, 2);                                                //   Создаём объект для работы по протоколу Modbus указывая объект шины UART-RS485 и вывод DE конвертера UART-RS485.
iarduino_MB_TDS sensor(modbus);                                                    //   Создаём объект для работы с TDS-метром указывая объект протокола Modbus.
                                                                                   //
void setup(){                                                                      //
     Serial.begin(9600); while(!Serial);                                           //   Инициируем передачу данных в монитор последовательного порта, указав его скорость.
     Serial1.begin(9600); while(!Serial1);                                         //   Инициируем работу с шиной UART-RS485 указав её скорость.
     modbus.begin();                                                               //   Инициируем работу по протоколу Modbus.
//   modbus.setTimeout(10);                                                        //   Указываем максимальное время ожидания ответа по протоколу Modbus.
//   modbus.setDelay(4);                                                           //   Указываем минимальный интервал между отправляемыми сообщениями по протоколу Modbus.
//   modbus.setTypeMB( MODBUS_RTU );                                               //   Указываем тип протокола Modbus: MODBUS_RTU (по умолчанию), или MODBUS_ASCII.
     sensor.begin(5);                                                              //   Инициируем работу с TDS-метром, указав его адрес. Если адрес не указан sensor.begin(), то он будет найден, но это займёт некоторое время.
//   sensor.setKt(0.02f);                                                          //   Указываем температурный коэффициент жидкости. Значение зависит от типа раствора. Используется для приведения S к EC. По умолчанию 0.02 = 2%.
//   sensor.setKp(0.5f);                                                           //   Указываем коэффициент пересчёта EC в TDS. Значение зависит от химического состава жидкости. Обычно применяют 0.5, 0.64 или 0.7. По умолчанию 0.5.
//   sensor.set_T(25);                                                             //   Указываем опорную температуру жидкости. Обычно используют 20°С или 25°С. По умолчанию 25°С.
}                                                                                  //
                                                                                   //
void loop(){                                                                       //
     sensor.set_t(25.0);                                                           //   Указываем модулю температуру жидкости в которую погружен щуп.
     delay(1000);                                                                  //
                                                                                   //
     Serial.print  ( F("Злектропроводность S = Ka * Vout^Kb = ")               );  //   Формула по которой модуль вычисляет удельную электропроводность жидкости.
     Serial.print  ( sensor.getKa()                                        , 3 );  //   Ka   - Множитель степенной функции (определяется при калибровке).
     Serial.print  ( " * "                                                     );  //
     Serial.print  ( sensor.getVout()                                      , 4 );  //   Vout - Напряжение на выходе усилителя.
     Serial.print  ( "^(-"                                                     );  //
     Serial.print  ( sensor.getKb()                                        , 3 );  //   Kb   - Модуль степени степенной функции (определяется при калибровке).
     Serial.print  ( ") = "                                                    );  //
     Serial.print  ( sensor.get_S()                                            );  //   S    - Рассчитанная удельная электропроводность жидкости.
     Serial.println( F(" мкСм/см.\r\n")                                        );  //
                                                                                   //
     Serial.print  ( F("Приведённая электропроводность EC = S/(1+Kt(t-T)) = ") );  //   Формула по которой модуль приводит электропроводность жидкости от текущей температуры t к опорной температуре T.
     Serial.print  ( sensor.get_S()                                            );  //   S    - Измеренная удельная электропроводность жидкости.
     Serial.print  ( " / (1+"                                                  );  //
     Serial.print  ( sensor.getKt()                                        , 4 );  //   Kt   - Температурный коэффициент жидкости (табличное значение зависит от типа раствора).
     Serial.print  ( "(25.00-"                                                 );  //   t    - Текущая температура жидкости (в начале кода функции loop мы отправляем модулю значение 25°С).
     Serial.print  ( sensor.get_T()                                        , 3 );  //   T    - Опорная температура жидкости (обычно T = 20°С или 25°С).
     Serial.print  ( ") = "                                                    );  //
     Serial.print  ( sensor.getEC()                                            );  //   EC   - Приведённая электропроводность жидкости.
     Serial.println( F(" мкСм/см.\r\n")                                        );  //
                                                                                   //
     Serial.print  ( F("Минерализация TDS = EC * Kp = ")                       );  //   Формула по которой модуль вычисляет общее количество растворенных твердых веществ.
     Serial.print  ( sensor.getEC()                                            );  //   EC   - Электропроводность жидкости приведённая к опорной температуре.
     Serial.print  ( " * "                                                     );  //
     Serial.print  ( sensor.getKp()                                        , 2 );  //   Kp   - Коэффициент пересчёта EC в TDS (зависит от химического состава жидкости, чаще всего используют 0.5, 0.64 или 0.7).
     Serial.print  ( " = "                                                     );  //
     Serial.print  ( sensor.getTDS()                                           );  //   TDS  - Общее количество растворенных твердых веществ.
     Serial.println( F(" ppm (мг/л воды).\r\n\r\n")                            );  //
}                                                                                  //
